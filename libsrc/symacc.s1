* SYMACC.S1 -- Symbol Table Access Module for Small, CRB, Feb 9, 2014
* 03/14/2014 CRB Add COLLS collision counter
* 03/20/2014 CRB Move MASKV initialization to initsym, other corrections
* 03/29/2014 CRB Add A2B40L for use with LEX
* 04/28/2014 CRB Fix bug in A2B40L and use MASKV for LEN
* 11/30/2014 CRB Correct comment in DCL TABLE
* 12/06/2014 CRB Add MASKW initialized in initsym
* 12/08/2014 CRB Further adjustment for negative packed characters
* 12/10/2014 CRB Add MOD40K = 53687091
* 12/12/2014 CRB Initialize VAL and TAG to 0 for new name
* 12/16/2014 CRB Adjust B402A for negative packed name

BEGIN SYMACC;
* Symbols are stored in an array called TABLE that is indexed by a hash
* function on the first six characters of a name. Each symbol occupies
* two 32-bit words in the table. The first word contains the compressed
* base 40 representation of the symbol, and the second word contains the
* 16-bit VAL and TAG words.

  ENTRY LOOKS,GETS,PUTS,GETWRD; * symbol table acces procedures
  ENTRY A2B40,A2B40L,B402A;     * ASCII, B40 conversion
  ENTRY COLLS;                  * Collision counter
  ENTRY MASKV,MASKW,MOD40K;

  SET TABSIZ=8191;              * prime < 2^13
  DCL TABLE(TABSIZ);            * table < 2^15 bytes, 4094 symbols
  DCL STRING(6);                * space to hold converted ASCII string
  DCL I,J,CHAR,LEN,B,CODE;      * local variables
  DCL MASKE=-2;                 * mask for even number
  DCL MASKV=65535;              * mask for low 16 bits
  DCL MASKW;                    * mask for signed 32 bits (init in initsym)
  DCL MOD40K;                   * use for negative mod 40: 53617091
  DCL COLFLG,COLLS=0;           * collision counter on insertion only
  DCL ANC;                      * anchor

* Convert ASCII STR to base 40 CODE packing 6 characters in 32 bit word
* Only upper case letters and decimal digits are allowed
* Return the 32-bit CODE value
******************************
PROC A2B40(STR);                * convert ASCII string to base 40
  LEN=STR;                      * get length of string
  IF LEN GT 6; THEN 
    LEN=6;                      * truncate length to 6
    ENDIF
  I=LEN;
  CODE=0;                       * init B40 code
  DO WHILE I GT 0;
    CHAR=STR(I);                * read charaters in reverse order
    IF CHAR LE 57; THEN 
      B=CHAR-21;                * digits 0..9 -> 27..36
    ELSE IF CHAR LE 90; THEN 
      B=CHAR-64;                * upper case A..Z -> 1..26
    ELSE IF CHAR LE 122; THEN 
      B=CHAR-96;                * lower case a..z -> 1..26
      ENDIF ENDIF ENDIF
    CODE=40*CODE+B;             * calculate base 40 code
    I=I-1;                      * decrement I for next character
    ENDDO
  RETURN CODE;                  * return accumulated base 40 code
  ENDPROC

* Use this version to grab a token from a substring
* SUBSTR contains length SHL 8 and anchor starting index
******************************
PROC A2B40L(SUBSTR,STR);        * convert ASCII string to base 40
  ANC=SUBSTR AND 255;           * get anchor starting index
  LEN=SUBSTR SHR 8 AND 255;     * get length of string
  IF LEN GT 6; THEN 
    LEN=6;                      * truncate length to 6
    ENDIF
  I=ANC+LEN-1;                    * start at end of token string
  CODE=0;                       * init B40 code
  DO WHILE I GE ANC;
    CHAR=STR(I);                * read charaters in reverse order
    IF CHAR LE 57; THEN 
      B=CHAR-21;                * digits 0..9 -> 27..36
    ELSE IF CHAR LE 90; THEN 
      B=CHAR-64;                * upper case A..Z -> 1..26
    ELSE IF CHAR LE 122; THEN 
      B=CHAR-96;                * lower case a..z -> 1..26
      ENDIF ENDIF ENDIF
    CODE=40*CODE+B;             * calculate base 40 code
    I=I-1;                      * decrement I for next character
    ENDDO
  RETURN CODE;                  * return accumulated base 40 code
  ENDPROC

* Convert base 40 code to ASCII in NAME
* No return value
*****************
PROC B402A(B40,NAME);           * convert base 40 to ASCII string
  B=B40;
  IF B LT 0; THEN               * adjust for negative base 40 code
      B=(B AND MASKW)+8;
      CHAR=B MOD 40;
      B=B/40+MOD40K;
    ELSE 
      CHAR=B MOD 40;
      B=B/40;
    ENDIF
  I=1;
  REPEAT 
    IF CHAR LE 26;
      THEN CHAR=CHAR+64;        * upper case letter
      ELSE CHAR=CHAR+21;        * decimal digit
      ENDIF
    NAME(I)=CHAR;               * put character in name string
    I=I+1;
    CHAR=B MOD 40;
    B=B/40;
    IF CHAR EQ 0;
      THEN EXIT
      ENDIF
    UNTIL I GT 6;
  NAME=I-1;                     * set string length
  RETURN
  ENDPROC

* Look up a packed symbol (WORDS) in TABLE
* Insert the new symbol with VAL and TAG if not already in TABLE
* If collision use next empty slot, if TABLE full return -1
* If existing entry found return VAL and TAG from TABLE
* Return value is index of entry in TABLE
*****************************************
PROC LOOKS(WORDS,VAL,TAG);      * look up or insert symbol
  COLFLG=0;                     * reset collision flag
  J=WORDS;
  IF J LT 0; THEN               * fix problem with negative WORDS
      J=(J AND MASKW)+32;       * remove sign bit, adjust remainder
    ENDIF
  I=(J MOD TABSIZ) AND MASKE;   * use only even index
  J=I;                          * save starting position
  REPEAT 
    IF TABLE(I) EQ 0;           * found empty slot
      THEN TABLE(I)=WORDS;      * insert symbol
        IF TAG;                 * new symbol name if TAG is zero
          THEN TABLE(I+1)=TAG SHL 16 OR VAL; * pack VAL and TAG
          ELSE TABLE(I+1)=0;    * set VAL and TAG to 0 for new name
	  ENDIF
        IF COLFLG; THEN         * if collision flag on then
          COLLS=COLLS+1;        * count collisions on insertion
          ENDIF
        RETURN I;
      ELSE IF TABLE(I) EQ WORDS; * found match
      THEN B=TABLE(I+1);        * retrieve VAL and TAG
        VAL=B AND MASKV;
        TAG=(B SHR 16) AND MASKV;
        RETURN I;
      ENDIF ENDIF
    I=I+2;                      * try next slot
    COLFLG=1;                   * set collision flag
    IF I GT TABSIZ;
      THEN I=I-TABSIZ;
      ENDIF
    UNTIL I EQ J;               * check for wrap to starting position
  RETURN -1;                    * table full
  ENDPROC

* Get VAL and TAG for this INDEX in TABLE
* No return value
*****************
PROC GETS(INDEX,VAL,TAG);       * get VAL and TAG for INDEX
  B=TABLE(INDEX+1);
  VAL=B AND MASKV;              * get VAL part
  TAG=(B SHR 16) AND MASKV;     * get TAG part
  RETURN
  ENDPROC

* Put new VAL and TAG for this INDEX in TABLE
* No return value
*****************
PROC PUTS(INDEX,VAL,TAG);       * put VAL and TAG at INDEX
  TABLE(INDEX+1)=(TAG SHL 16) OR VAL;
  RETURN
  ENDPROC

* Get packed symbol (WDS) for this index in TABLE
* No return value
*****************
PROC GETWRD(INDEX,WDS);         * get base 40 code at INDEX
  WDS=TABLE(INDEX);
  RETURN
  ENDPROC

END
