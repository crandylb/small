* IFORM -- CONVERT INTEGER TO DECIMAL CHARACTER STRING AS IN I FORMAT
BEGIN IFORM;                  * CRB OCT 31, 1981
     ENT IFORM;
     DCL N,Q;
     DCL PTR;                 * TEMPORARY STORAGE FOR CONVERSION
     DCL STAR=('*');
     DCL MINUS=('-');
*---------------
PROC IFORM(NUM,BUFF);
     N=NUM;                   * GET NUMBER TO BE CONVERTED
     PTR=BUFF;                * POINTER INTO USERS BUFFER
     IF N LT 0; THEN N=-N; ENDIF
     REPEAT                   * REPEAT UNTIL N IS CONVERTED
       IF PTR LE 0; THEN GO TO TOOBIG ENDIF
       Q=N/10;                * GET NEXT QUOTIENT
       BUFF(PTR)=N-10*Q+'0';  * CONVERT REMAINDER TO CHAR
       N=Q;
       PTR=PTR-1;
       UNTIL N EQ 0;
     IF NUM LT 0; THEN 
       IF PTR LE 0; THEN GO TO TOOBIG;
       ELSE BUFF(PTR)=MINUS;  * INSERT MINUS SIGN IF IT FITS
         PTR=PTR-1;
         ENDIF
       ENDIF
     DO WHILE PTR GT 0;       * BLANK OUT ANY UNUSED PART OF BUFF
       BUFF(PTR)=' ';
       PTR=PTR-1;
       ENDDO
     RETURN
LABEL TOOBIG                  * CONVERTED NUMBER TOO BIG FOR BUFF
     BUFF(1)=STAR;            * SET FIRST CHAR = * TO NOTE ERROR
     RETURN
   ENDPROC
 END IFORM
